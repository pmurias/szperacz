#!/usr/bin/perl
use v5.10;
use Storable;
use Tokenizer;
use Postings;
use Term::ANSIColor qw(:constants);
use List::MoreUtils qw(any uniq);
use Encode;
use Morfologik;
use Getopt::Long;
use Stemmer;
my $stemmer = 0;
my $compressed = 0;
GetOptions(stemmer => \$stemmer,compressed=> \$compressed);
use warnings;
use strict;
sub debug {
    #say "# ",@_;
}
debug "creating postings...";
my $filename = "index/postings_full";
if ($compressed) {
    $filename = "index/postings";
}

my $stemmer_part = '.stemming';
$stemmer_part = '' unless $stemmer;
$filename .= '.stemming' if ($stemmer);
$filename .= '.compressed' if ($compressed);
say $filename;
my $postings = PostingsPtr::create($filename);

binmode *STDOUT, ':encoding(UTF-8)';
binmode *STDIN,  ':encoding(UTF-8)';

debug "loading tokIDs...";
my $h = retrieve("index/tokIDs$stemmer_part");
debug "loading morfologik...";
Morfologik::load() unless $stemmer;
Stemmer::load_stemming() if $stemmer;
debug "enter query";

sub search {
    my $tokID  = shift;
#    my $tokID = $h->{$word};
#    say "tokID $word = $tokID";
    if ($tokID != -1) {
        debug "searching for tokID: $tokID";
        my $ret = $postings->search($tokID);
        debug "searched";
        $ret;
    } else {
        $postings->empty();
    }
}

sub to_id {
    my $tok = shift;
    $h->{$tok} // -1;
}

sub possible {
    my $tok = shift;
    if ($stemmer) {
        to_id(Stemmer::stem($tok));
    } else {
        map {to_id($_)} @{Morfologik::get(lc $tok)};
    }
}

sub any_of {
    my @or = map {possible($_)} @_;
    my $result = search( shift @or );
    for my $w (@or) {
        $result = $result->or( search($w) );
    }
    $result;
}

sub or_query {
    my ($q) = @_;
    any_of( split( '\|', $q ));
}

open (my $titles,'index/titles');
binmode $titles,  ':encoding(UTF-8)';

my @titles = <$titles>;
sub title {
    my $docID = shift;
    my $title = $titles[$docID];
    chomp($title);
    $title;
}

sub report {
    my ($query,$out,$result) = @_;
    my @documents = $result->flatten;
    say $out "QUERY: $query TOTAL: ", $#documents + 1;
    for my $docID (@documents) {
        say $out title($docID);
    }
}
sub process_query {
    my ($q,$out) = @_;
    chomp($q);
    $q = lc $q;
    if ( $q =~ /"(.*)"/ ) {
        my @parts = split(' ',$1);
        my $result = any_of( shift @parts );
        for my $w (@parts) {
            $result = $postings->phrase($result, any_of($w) );
        }
        report($q,$out,$result);
    }
    else {
        my @ands = split( ' ', $q );
        my $result = or_query( shift @ands );
        for my $and (@ands) {
            $result = $result->and( or_query($and) );
        }
        report($q,$out,$result);
    }
}

if ($ARGV[0] && $ARGV[1]) {
    open (my $in,$ARGV[0]);
    binmode $in,':encoding(UTF-8)';

    open (my $out,'>',$ARGV[1]);
    binmode $out,':encoding(UTF-8)';

    while (my $q = <$in>) {
        process_query($q,$out);
    }
} else {
    use Term::ReadLine;
    my $term = new Term::ReadLine 'xxx';
    my $prompt = "? ";
    binmode $term->OUT, ':encoding(UTF-8)';

    while ( defined (my $q = decode('utf8',$term->readline($prompt))) ) {
        $term->addhistory($q) if $q =~ /\S/;
        process_query($q,$term->OUT);
    }
}

